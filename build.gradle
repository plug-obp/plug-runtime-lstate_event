version='1.0.0'
apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
    maven { url "${OBP2ReadOnlyMavenRepository}" }
}

compileJava.options.encoding = 'UTF-8'
sourceSets {
    main {
        java { srcDir  'src' }
        resources { srcDir 'resources'}
    }
    test {
        java { srcDir 'tests/src' }
        resources { srcDir 'tests/resources'}
    }
}

publishing {
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            url = "${OBP2PublishMavenRepository}"
        }
    }
    publications {
        jar(MavenPublication) {
            groupId = 'fr.ensta-bretagne'
            from components.java
        }
    }
}

configurations {
   antlr4
   compile.extendsFrom antlr4
}

dependencies {
   antlr4  group: 'org.antlr', name: 'antlr4' , version: '4.5.+'
   compile group: 'com.fasterxml.jackson.core', name:'jackson-databind', version:'2.7.2'
   compile group: 'fr.ensta-bretagne', name: 'obp2-runtime', version: '1.0.0'
    testCompile group: 'fr.ensta-bretagne', name: 'LPropositionalLogic', version: '1.0.0'
    testCompile group: 'fr.ensta-bretagne', name: 'LTL', version: '1.0.0'
   testCompile group: 'fr.ensta-bretagne', name: 'obp2-runtime-buchikripke', version: '1.0.0'
    testCompile group: 'fr.ensta-bretagne', name: 'obp2-algorithms', version: '1.0.0'
    testCompile group: 'fr.ensta-bretagne', name: 'obp2-visualisation', version: '1.0.0'
    testCompile group: 'junit', name: 'junit', version: '4.7'
}

String antlr4OutputPath(String path) {
   if (System.properties['os.name'].replaceAll(' ', '').toLowerCase().contains('win')) {
      return file(path + '/grammar')
   }
   return file(path)
}
def generatedPath = file('generated')
generatedPath.mkdir()
task generateAntlr4Parser(type: JavaExec) {
   classpath configurations.antlr4
   main = "org.antlr.v4.Tool"
   args = ["grammar/SEDiag.g4"
           , "-package"
           , "obp2.language.state_event.diagnosis.dsl.grammar"
           , "-o"
           , antlr4OutputPath("generated/obp2/language/state_event/diagnosis/dsl")]
}

sourceSets.main.java.srcDir 'generated'
compileJava.dependsOn generateAntlr4Parser

project.ext["ideaGroupName"]="plug/languages"

apply plugin: 'java-library-distribution'
